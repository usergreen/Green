dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(grsync, 1.3.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

CFLAGS="-Wall -rdynamic $CFLAGS"
LIBS="-lm $LIBS"

AC_USE_SYSTEM_EXTENSIONS
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_ARG_ENABLE([gtk3],
    [AC_HELP_STRING([--enable-gtk3],
        [compile grsync against gtk+ 3.0 (default: yes)])],
    [enable_gtk3="${enableval}"],
    [enable_gtk3="yes"]
)

XMLFILE="grsync.glade"
AC_DEFINE_UNQUOTED(XMLFILE, ["$XMLFILE"], [UI Description XML File])

if test x"$enable_gtk3" = x"yes"; then
		GTK_API_VERSION="3.0"
else
		GTK_API_VERSION="2.0"
fi
pkg_modules="gtk+-$GTK_API_VERSION >= 2.16.0"
OTHER_DESKTOP_ENTRIES="Icon=grsync.png"
MIMEINFO_XMLNS="xmlns='http://www.freedesktop.org/standards/shared-mime-info'"
MIME_OSSOCAT=""

PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)
AC_SUBST(OTHER_DESKTOP_ENTRIES)
AC_SUBST(MIMEINFO_XMLNS)
AC_SUBST(MIME_OSSOCAT)
AC_SUBST(XMLFILE)

AC_ARG_ENABLE(unity,
	AS_HELP_STRING([--enable-unity],[Build support for integration in Unity launcher [[default=no]]]),
	[enable_unity=$enableval],
	[enable_unity="no"])
if test x"$enable_unity" = "xyes"; then
	if test "x$with_localinstall" = "xyes"; then
		DBUSSERVICEDIR="${datadir}/dbus-1/services/"
	else
		DBUSSERVICEDIR=`$PKG_CONFIG --variable=session_bus_services_dir dbus-1`
	fi
	AC_SUBST(DBUSSERVICEDIR)

	PKG_CHECK_MODULES(UNITY,
		dee-1.0
		dbusmenu-glib-0.4
		unity)
	AC_SUBST(UNITY_CFLAGS)
	AC_SUBST(UNITY_LIBS)
	AC_DEFINE(HAVE_UNITY, 1, [Unity launcher support])
	CFLAGS="$UNITY_CFLAGS $CFLAGS"
	LIBS="$UNITY_LIBS $LIBS"
fi
AM_CONDITIONAL(HAVE_UNITY, test x"$enable_unity" = xyes)

AC_PROG_INTLTOOL()

GETTEXT_PACKAGE=grsync
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])

dnl Add the languages which your application supports here.
ALL_LINGUAS="nl_NL it_IT zh_CN fr_FR sv_SE nb_NO tr_TR ru_RU de_DE es_ES cs_CZ gl_ES ca_ES pt_BR id_ID hr_HR zh_TW hu_HU el_GR pt_PT"
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
grsync.spec
grsync.desktop
grsync.xml
it.opbyte.grsync.service
])

